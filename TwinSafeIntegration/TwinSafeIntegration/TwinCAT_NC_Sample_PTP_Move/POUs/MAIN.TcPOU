<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="MAIN" Id="{0c6c160e-f7a8-43f6-9511-ea953652025c}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM MAIN
VAR
	Axis1 : AXIS_REF;					(* axis reference data structure *)
	state: MoveState_t;				(* state machine state *)

	PowerAxis1: MC_Power;
	PowerAxis1Out: ST_McOutputs;		(* debug function block output data *)

	MoveAxis1: MC_MoveAbsolute;
	MoveAbsoluteOut1: ST_McOutputs;	(* debug function block output data *)

	MoveAxis2: MC_MoveAbsolute;
	MoveAbsoluteOut2: ST_McOutputs;	(* debug function block output data *)

	Reset: MC_Reset;
	ResetOut : ST_McOutputs;			(* debug function block output data *)
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[	(* update the axis status at the beginning of each cycle *)
		Axis1.ReadStatus();

	(* move axis using a state machine *)
	CASE state OF

		MOVESTATE_INIT :	(* initialisation *)
			(* initialize all function blocks *)
			MoveAxis1.Execute := FALSE;
			MoveAxis2.Execute := FALSE;
			Reset.Execute := FALSE;
			state := MOVESTATE_ENABLE;


		MOVESTATE_ENABLE :
			PowerAxis1.Enable := TRUE;
			PowerAxis1.Enable_Positive := TRUE;
			PowerAxis1.Enable_Negative := TRUE;
			IF PowerAxis1.Status THEN
				state := MOVESTATE_FORWARD;
			ELSIF PowerAxis1.Error THEN
				state := MOVESTATE_ERROR;
			END_IF

		MOVESTATE_FORWARD :		(* start to first position *)
			MoveAxis1.Position := 1500;		(* start to intermediate tartet position *)
			MoveAxis1.Velocity := 500;		(* medium velocity level *)
			MoveAxis1.BufferMode := MC_Buffered;
			MoveAxis1.Execute := TRUE;
			IF MoveAxis1.Active  THEN
				(* axis is executing job but is not yet finished *)
				MoveAxis1.Execute := FALSE;
				(* leave this state and buffer a second command *)
				state := MOVESTATE_NEWTARGETPOSITION;
			ELSIF MoveAxis1.CommandAborted OR MoveAxis1.Error THEN
				state := MOVESTATE_ERROR;
			END_IF

		MOVESTATE_NEWTARGETPOSITION :		(* change target position while moving *)
			(* start a buffered motion command while axis is moving *)
			MoveAxis2.Position := 5000;		(* start to final target *)
			MoveAxis2.Velocity := 1500;		(* increase velocity *)
			MoveAxis2.BufferMode := MC_BlendingPrevious; 	(* use velocity of previous command till intermediate target position position *)
			MoveAxis2.Execute := TRUE;
			IF MoveAxis2.Done THEN
				MoveAxis2.Execute := FALSE;
				state := MOVESTATE_BACKWARDS;
			ELSIF MoveAxis2.CommandAborted OR MoveAxis2.Error THEN
				state := MOVESTATE_ERROR;
			END_IF

		MOVESTATE_BACKWARDS :	(* move back to initial position *)
			MoveAxis1.Position := 100;		(* position close to initial position *)
			MoveAxis1.Velocity := 2000;		(* high velocity *)
			MoveAxis1.BufferMode := MC_Buffered;
			MoveAxis1.Execute := TRUE;
			IF MoveAxis1.Active THEN
				(* axis is executing job but is not yet finished *)
				MoveAxis1.Execute := FALSE;
				(* leave this state and buffer a second command *)
				state := MOVESTATE_BACKWARDSSLOW;
			ELSIF MoveAxis1.CommandAborted OR MoveAxis1.Error THEN
				state := MOVESTATE_ERROR;
			END_IF

		MOVESTATE_BACKWARDSSLOW :	(* move back to initial position *)
			(* start a buffered motion command while axis is moving *)
			MoveAxis2.Position := 0;	(* back to initial position *)
			MoveAxis2.Velocity := 100; 	(* slow velocity *)
			MoveAxis2.BufferMode := MC_BlendingNext; 	(* use velocity from first target position *)
			MoveAxis2.Execute := TRUE;
			IF MoveAxis2.Done THEN
				MoveAxis2.Execute := FALSE;
				state := MOVESTATE_FORWARD;
			ELSIF MoveAxis2.CommandAborted OR MoveAxis2.Error THEN
				state := MOVESTATE_ERROR;
			END_IF

		MOVESTATE_ERROR :
			IF Axis1.Status.Error THEN
				state := MOVESTATE_RESET;	(* axis error requires reset *)
			ELSE
				state := MOVESTATE_INIT;		(* function block errors don't need a reset *)
			END_IF

		MOVESTATE_RESET :
			Reset.Execute := TRUE;
			IF Reset.Done THEN
				state := MOVESTATE_INIT;
			ELSIF Reset.Error THEN
				state := MOVESTATE_INIT; (* can't do anything here *)
			END_IF

	END_CASE

//		ActPower();	(* call the power function block *)
//		ActMove();	(* call the move function block *)
//		ActReset();	(* call the reset function block *)
]]></ST>
    </Implementation>
    <LineIds Name="MAIN">
      <LineId Id="17" Count="11" />
      <LineId Id="131" Count="0" />
      <LineId Id="29" Count="85" />
    </LineIds>
  </POU>
</TcPlcObject>